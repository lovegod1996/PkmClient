apply plugin: 'com.android.application'

def gitVersionCode() {
    def cmd = 'git rev-list HEAD --first-parent --count'
    cmd.execute().text.trim().toInteger()
}

def gitVersionTag() {
    def cmd = 'git describe --tags'
    def version = cmd.execute().text.trim()

    def pattern = "-(\\d+)-g"
    def matcher = version =~ pattern

    if (matcher) {
        version = version.substring(0, matcher.start()) + "." + matcher[0][1]
    } else {
        version = version + ".0"
    }

    return version
}

Properties props = new Properties()
props.load(new FileInputStream(file("signing.properties")))
android {
    signingConfigs {
        release {
            keyAlias props['KEY_ALIAS']
            keyPassword props['KEY_PASSWORD']
            storeFile file(props['KEYSTORE_FILE'])
            storePassword props['KEYSTORE_PASSWORD']
        }
    }
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false

    }

    defaultConfig {
        applicationId "com.example.prmgclient"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        debug {
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"
        }
        release {
            //引用正式签名
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name.equals('release')) {
            variant.mergedFlavor.versionCode = gitVersionCode()
            variant.mergedFlavor.versionName = gitVersionTag()
        }
    }

    useLibrary 'org.apache.http.legacy'
    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
    sourceSets { main { jniLibs.srcDirs 'libs' } }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.0.0'
    testCompile 'junit:junit:4.12'
    compile files('libs/alibaba.jar')
    compile files('libs/commons-codec.jar')
    compile files('libs/commons-lang3-3.0-beta.jar')
    compile files('libs/httpcore-4.2.4.jar')
    compile 'com.google.android.gms:play-services-appindexing:8.4.0'
    compile files('libs/baidumapapi_base_v4_1_1.jar')
    compile files('libs/baidumapapi_cloud_v4_1_1.jar')
    compile files('libs/baidumapapi_map_v4_1_1.jar')
    compile files('libs/baidumapapi_radar_v4_1_1.jar')
    compile files('libs/baidumapapi_search_v4_1_1.jar')
    compile files('libs/baidumapapi_util_v4_1_1.jar')
    compile files('libs/locSDK_6.13.jar')
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}